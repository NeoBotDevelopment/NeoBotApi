plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.6"
}

group 'page.nafuchoco'
version '2.3.0'

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}
javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}
task beforeDeploy {
    dependsOn('sourcesJar', 'javadocJar', 'assemble')
}
publishing {
    publications {
        mavenAar(MavenPublication) {
            from components.java
            afterEvaluate {
                artifact javadocJar
                artifact sourcesJar
            }
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.dv8tion.net/releases' }
}

dependencies {
    // Annotation
    compileOnly 'org.jetbrains:annotations:26.0.2'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2' // Deprecated - Consider com.github.spotbugs:spotbugs-annotations

    // Logger
    api 'org.slf4j:slf4j-api:2.0.17'

    // ClientLib
    api('net.dv8tion:JDA:5.3.1')

    // Parser
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.18.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.18.3'

    // Commons Library
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    // SQL
    api 'com.zaxxer:HikariCP:6.3.0'
}
